import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'jacoco'
apply plugin: "com.eriwen.gradle.js"

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
    mavenCentral()
}

dependencies {

    compile (
            // Spring
            [group: 'org.springframework',              name: 'spring-core',            version: springVersion],
            [group: 'org.springframework',              name: 'spring-webmvc',          version: springVersion],

            // Jackson Mapper
            [group: 'com.fasterxml.jackson.core',       name: 'jackson-core',           version: jacksonMapperVersion],
            [group: 'com.fasterxml.jackson.core',       name: 'jackson-databind',       version: jacksonMapperVersion],

            // GAE
            [group: 'com.google.appengine',             name: 'appengine-api-1.0-sdk',  version: appengineVersion],
            [group: 'com.google.appengine',             name: 'appengine-api-labs',     version: appengineVersion],
            [group: 'com.googlecode.objectify',         name: 'objectify',              version: objectifyVersion],

            // Utils
            [group: 'org.apache.commons',               name: 'commons-lang3',          version: commonsLang3Version],
            [group: 'commons-io',                       name: 'commons-io',             version: commonsIoVersion],
            [group: 'com.google.guava',                 name: 'guava',                  version: googleGuavaVersion],
            [group: 'org.hamcrest',                     name: 'hamcrest-library',       version: hamcrestVersion],
    )


    providedCompile (
            // Servlet
            [group: 'javax.servlet',                    name: 'servlet-api',            version: servletApiVersion]
    )

    testCompile (
            [group: 'junit',                            name: 'junit',                  version: junitVersion],
            [group: 'org.mockito',                      name: 'mockito-all',            version: mockitoVersion],
            [group: 'org.hamcrest',                     name: 'hamcrest-integration',   version: hamcrestVersion],
            [group: 'org.springframework',              name: 'spring-test',            version: springVersion]
    )
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.eriwen:gradle-js-plugin:1.12.1"
    }
}


task processAppEngine(type: Copy) {
    from('src/main/resources') {
        include 'appengine-web.xml'
        filter(ReplaceTokens, tokens:
                [
                        "gae.version": project.hasProperty('gaeVersion') ? project.gaeVersion : 'development'
                ]
        )
    }
    into 'src/main/webapp/WEB-INF'
}

processResources{
    exclude 'appengine-web.xml'
}

processResources.dependsOn processAppEngine

task unzipWar << {
    ant.unzip(src: war.archivePath, dest: "$buildDir/exploded")
}

war.finalizedBy unzipWar

pmd {
    ruleSetFiles = files("$projectDir/config/pmd/pmd.xml")
    consoleOutput = true
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}