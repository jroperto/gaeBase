import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'war'

group       = 'com.github.jroperto'
version     = '0.0.1'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

ext {

    springVersion       = '4.0.2.RELEASE'
    objectifyVersion    = '5.0.3'
    appengineVersion    = '1.9.10'
}

repositories {
    mavenCentral()
}

dependencies {

    // Spring
    compile "org.springframework:spring-webmvc:$springVersion"

    // GAE
    compile "com.google.appengine:appengine-api-1.0-sdk:$appengineVersion",
            "com.google.appengine:appengine-api-labs:$appengineVersion"

    // Objectify
    compile "com.googlecode.objectify:objectify:$objectifyVersion"

    // Others
    compile 'org.apache.commons:commons-lang3:3.2.1'
}

task processAppEngine(type: Copy) {
    from('src/main/resources') {
        include 'appengine-web.xml'
        filter(ReplaceTokens, tokens:
                [
                        "gae.version": project.gaeVersion
                ]
        )
    }
    into 'src/main/webapp/WEB-INF'
}

processResources{
    exclude 'appengine-web.xml'
}

processResources.dependsOn(processAppEngine)

gradle.taskGraph.afterTask { Task task, TaskState state ->
    if (task.name == 'war') {
        ant.unzip(src: war.archivePath, dest: "$buildDir/exploded")
    }
}

// PMD
pmd {
    toolVersion = '5.0.5'
    ruleSetFiles = files("$projectDir/config/pmd/pmd.xml")
    ruleSets = []
}

gradle.taskGraph.afterTask { Task task, TaskState state ->
    if ((task.name == 'pmdMain' || task.name == 'pmdTest') && state.failure) {
        // print errors
        def outFile = task.name == 'pmdMain' ? 'main.xml' : 'test.xml'
        def reportFile = file("${buildDir}/reports/pmd/${outFile}")

        if(reportFile.exists()) {
            def result = new XmlParser().parse(reportFile)

            println ''
            result.file.each { file ->
                file.violation.each { violation ->
                    println "${file.'@name'}:${violation.'@beginline'}: ${violation.text()}"
                }
            }
        }


    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}